plugins {
    id 'org.springframework.boot' version '2.5.4'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'application'
}


group = 'com.nomad'
version = '0.0.1'
sourceCompatibility = '11'

application {
    mainClass = 'com.nomad.m1.Application'
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:2020.0.3"
    }
}

dependencies {
    implementation project(':common')
    implementation 'org.springframework.boot:spring-boot-starter'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework:spring-jdbc:5.3.7'
    implementation 'com.zaxxer:HikariCP:4.0.3'
    implementation 'org.postgresql:postgresql:42.2.20'
}

run {
    classpath += files('src/main/dist/conf')
}

startScripts {
    classpath += files('conf')
    doLast {
        unixScript.text = unixScript.text
                .replaceAll('APP_HOME=', 'export APP_HOME=')
                .replaceAll('\\$APP_HOME/lib/conf', '\\$APP_HOME/conf')
        windowsScript.text = windowsScript.text
                .replaceAll('%APP_HOME%\\\\lib\\\\conf', '%APP_HOME%\\\\conf')
    }
}